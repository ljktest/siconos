
SET_SOURCE_FILES_PROPERTIES(Numerics.i PROPERTIES CPLUSPLUS ON)
SET_SOURCE_FILES_PROPERTIES(Kernel.i PROPERTIES CPLUSPLUS ON)
SET_SOURCE_FILES_PROPERTIES(Numerics.i Kernel.i PROPERTIES SWIG_FLAGS "-Wall;-DSWIGA")

# do not skip the full RPATH for the build tree
SET(CMAKE_SKIP_BUILD_RPATH FALSE)

# when building, don't use the install RPATH already
# (but later on when installing)
SET(CMAKE_BUILD_WITH_INSTALL_RPATH FALSE) 

# the RPATH to be used when installing
SET(CMAKE_INSTALL_RPATH "${CMAKE_INSTALL_PREFIX}/lib")

# don't add the automatically determined parts of the RPATH
# which point to directories outside the build tree to the install RPATH
SET(CMAKE_INSTALL_RPATH_USE_LINK_PATH TRUE)

SWIG_ADD_MODULE(Numerics python Numerics.i)
SWIG_ADD_MODULE(Kernel python Kernel.i)

SWIG_LINK_LIBRARIES(Numerics ${PYTHON_LIBRARIES} 
  ${SiconosNumerics_LIBRARIES} 
  ${FORTRAN_LIBRARIES})

SWIG_LINK_LIBRARIES(Kernel ${PYTHON_LIBRARIES} 
  ${SiconosKernel_LIBRARIES} 
  ${SiconosNumerics_LIBRARIES} 
  ${FORTRAN_LIBRARIES} 
  ${Boost_LIBRARIES})


# get python dist-packages
# warning python may not know cmake_install_prefix

execute_process ( COMMAND 
  python -c "from distutils.sysconfig import get_python_lib; print get_python_lib()" 
  OUTPUT_VARIABLE PYTHON_DIST_PACKAGES 
  OUTPUT_STRIP_TRAILING_WHITESPACE)

MESSAGE(STATUS "PYTHON_DIST_PACKAGES : ${PYTHON_DIST_PACKAGES}")

INSTALL(PROGRAMS ${CMAKE_CURRENT_BINARY_DIR}/Numerics.py 
  DESTINATION ${PYTHON_DIST_PACKAGES}/Siconos)

INSTALL(PROGRAMS ${CMAKE_CURRENT_BINARY_DIR}/Kernel.py 
  DESTINATION ${PYTHON_DIST_PACKAGES}/Siconos)

CONFIGURE_FILE(__init__.py.in ${CMAKE_CURRENT_BINARY_DIR}/__init__.py)

INSTALL(PROGRAMS ${CMAKE_CURRENT_BINARY_DIR}/__init__.py 
  DESTINATION ${PYTHON_DIST_PACKAGES}/Siconos)

MESSAGE(STATUS "swig modules: ${SWIG_MODULE_Numerics_REAL_NAME},  ${SWIG_MODULE_Kernel_REAL_NAME}")

INSTALL(TARGETS ${SWIG_MODULE_Numerics_REAL_NAME} ${SWIG_MODULE_Kernel_REAL_NAME}
  DESTINATION ${PYTHON_DIST_PACKAGES}/Siconos)

INSTALL(FILES ${CMAKE_CURRENT_SOURCE_DIR}/KernelTypes.i
  DESTINATION share/siconos-front-end/swig)

ENABLE_TESTING()

ADD_TEST(lcp py.test ${CMAKE_CURRENT_SOURCE_DIR}/tests/test_lcp.py)
ADD_TEST(friction_contact py.test ${CMAKE_CURRENT_SOURCE_DIR}/tests/test_friction_contact.py)

SUBDIRS(tests)
